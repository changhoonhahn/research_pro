function read_mf_isedfit_chh, field, supergrid=supergrid, rows=rows, $
  post=post, parent=parent, test=test, isedfile=isedfile
; jm10aug23ucsd - read the desired output from ISEDFIT

; with /PARENT read the output of BUILD_MF_PARENT, otherwise read the
; full structures generated by MF_ISEDFIT    
    
    if keyword_set(parent) then mfpath = get_path(/parent) else $
      mfpath = get_path(/isedfit)
    if (n_elements(field) eq 0) then begin
       splog, 'FIELD input required!'
       return, -1
    endif

    if (n_elements(supergrid) eq 0) then supergrid = '1'
    super = get_mf_supergrid(supergrid,nsuper=nsuper)
    nsuper = n_elements(super)

; build the filename and then read
    superstring = string(super.supergrid,format='(I2.2)')
    sfhgridstring = string(super.sfhgrid,format='(I2.2)')
    redcurvestring = redcurve2string(super.redcurve)
    synthmodels = strtrim(super.synthmodels,2)
    imf = strtrim(super.imf,2)

    if keyword_set(test) then tprefix = 'test_' else tprefix = ''
    if (strtrim(redcurvestring,2) ne '') then redcurvestring = redcurvestring+'_'
    
    if keyword_set(parent) then begin
       isedfile = mfpath+'ised_parent_supergrid'+superstring+'_'+field+'.fits.gz'
    endif else begin
       isedfile = mfpath+tprefix+field+'_'+synthmodels+'_'+imf+'_'+$
         redcurvestring+'sfhgrid'+sfhgridstring+'.fits.gz'
    endelse
    isedfile_post = mfpath+tprefix+field+'_'+synthmodels+'_'+imf+'_'+$
      redcurvestring+'sfhgrid'+sfhgridstring+'_post.fits.gz'

    if (file_test(isedfile) eq 0) then begin
       splog, 'iSEDFIT file '+isedfile+' not found!'
       return, -1
    endif
    splog, 'Reading '+isedfile
    ised = mrdfits(isedfile,1,rows=rows)

; optionally read the posterior distribution (just the rows we care
; about) 
    if arg_present(post) then begin
       if (file_test(isedfile_post) eq 0) then begin
          splog, 'iSEDFIT file '+isedfile_post+' not found!'
          return, -1
       endif
       splog, 'Reading '+isedfile_post
       post = mrdfits(isedfile_post,1,rows=ised.isedfit_id)
    endif
    
return, ised
end
